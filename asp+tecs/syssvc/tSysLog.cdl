/*
 *  @(#) $Id: tSysLog.cdl 1146 2010-10-13 07:36:12Z ritsumei-takuya $
 */

/*
 *		システムログ機能のコンポーネント記述ファイル
 */
import_C("t_syslog.h");

signature sSysLog {
	ER		write([in] uint_t priority, [in] const SYSLOG *p_syslog);
	ER_UINT	read([out] SYSLOG *p_syslog);
	ER		mask([in] uint_t logMask, [in] uint_t lowMask);
	ER		refer([out] T_SYSLOG_RLOG *pk_rlog);
};

[singleton]
celltype tSysLog {
	entry sSysLog	eSysLog;
	call sPutLog	cPutLog;		/* 低レベル出力との接続 */

	attr {
		uint_t	logBufferSize;			/* ログバッファサイズ */
	};
	var {
		[size_is(logBufferSize)] SYSLOG	*logBuffer;	/* ログバッファ */
		uint_t	count = 0;			/* ログバッファ中のログの数 */
		uint_t	head = 0;			/* 先頭のログの格納位置 */
		uint_t	tail = 0;			/* 次のログの格納位置 */
		uint_t	lost = 0;			/* 失われたログの数 */
		uint_t	logMask = 0;		/* ログバッファに記録すべき重要度 */
		uint_t	lowMaskNot = 0;     /* 低レベル出力すべき重要度 */
	};
	FACTORY {
		/*
         * Syslogを利用する場合にMakefileを出力
		 */
		write("Makefile.tecsgen", "USE_SYSLOG = ture");
	};
};
